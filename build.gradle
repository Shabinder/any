// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://androidx.dev/snapshots/builds/8593961/artifacts/repository' }
    }
    dependencies {
        classpath libs.android.gradlePlugin
        classpath libs.kotlin.gradlePlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11

            // Avoid compile error when implementing a ViewModelProvider.Factory
            // https://issuetracker.google.com/issues/217593040
            freeCompilerArgs += [
                    "-Xjvm-default=all",
                    "-opt-in=kotlin.RequiresOptIn",
            ]

            if (project.findProperty("enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}